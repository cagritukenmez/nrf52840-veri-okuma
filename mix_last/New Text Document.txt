#include "ads.h"
#include "ledController.h"
#include "sistem.h"
#include <bluefruit.h>
#include <Adafruit_LittleFS.h>
#include <InternalFileSystem.h>
#include <ArduinoJson.h>

// BLE Service
BLEDfu  bledfu;  // OTA DFU service
BLEDis  bledis;  // device information
BLEUart bleuart; // uart over ble
BLEBas  blebas;  // battery

extern sistem mysistem;

ledController myleds;

struct data_box{
  bool state;
  uint32_t time_open;
  uint32_t time_delay;
  uint16_t time_read;
  int8_t pin;
}data_box[4];


unsigned long baslangiczamani;
unsigned long prev;
unsigned long json_delay = 20; //ms cinsinden belirli sürede bir json gönderme ayarı, şuanda kullanım dışı.
unsigned long adc_toplayici=0;
int adc_read_counter=0;
int itr=0;

bool dongu_led[4]={0,0,0,0};
bool dongu_delay[4]={0,0,0,0};
bool dongu_read_finish[4]={0,0,0,0};
bool callback=false;
bool starter_controller=false;

String rxBuffer = "";           // Gelen parçaları biriktirir
String gonderilecek_json="";
void setup() {
  Serial.begin(9600);
  while(!Serial); // bazı nRF kartlarda gerekli
  printResetReason();
  Serial.println("Program basladi");

  Serial.println("Bluefruit52 BLEUART JSON sender");//checkpoint 1
  Bluefruit.autoConnLed(true);
  Bluefruit.configPrphBandwidth(BANDWIDTH_MAX);
  Bluefruit.begin();
  Bluefruit.setTxPower(4);
  Bluefruit.Periph.setConnectCallback(connect_callback);
  Bluefruit.Periph.setDisconnectCallback(disconnect_callback);
  bledfu.begin();
  bledis.setManufacturer("Adafruit Industries");
  bledis.setModel("Bluefruit Feather52");
  bledis.begin();
  bleuart.begin();
  Serial.println("Bluefruit52 BLEUART JSON sender");//checkpoint 2
  bleuart.setRxCallback(uart_rx_callback);//her veri gelişinde bu fonksiyon çalışır
  blebas.begin();
  blebas.write(100);
  startAdv();

  myleds.begin();
  mysistem.myAds.begin();
}

void loop() {
  if(!mysistem.myAds.is_adc_started) {
    mysistem.myAds.begin();
  }

  if (Bluefruit.connected()) {
    myleds.update(itr);
    digitalWrite(22,LOW);
    if(callback){
      rxBuffer.trim();
      Serial.print(rxBuffer);
      parseJsonBuffer(rxBuffer);
      rxBuffer="";
      Serial.print("cp  7");
      callback=false;
    }


    if(mysistem.state && mysistem.myAds.read_state[itr]){

      //itr ile döngüyü kontrol et 3 bool tipi ile kontrolü sağla 
      if(!dongu_led[itr] && !dongu_delay[itr] && !dongu_read_finish[itr] && !starter_controller){
        //en başta buraya girer ve başlangıç ayarları yapılır.
        starter_controller =true;
        digitalWrite(itr+4,HIGH);
        prev=millis();
        Serial.print("cp1 ");
        adc_read_counter =0;
        adc_toplayici=0;
      }
      if(millis()-prev > myleds.leds[itr].kalansure&& !dongu_led[itr]){
        digitalWrite(itr+4,LOW);
        dongu_led[itr]=true;
        Serial.print("cp2 ");
      }
      if(dongu_delay[itr] && !dongu_read_finish[itr]){
        //delay işlemi bittiyse içeri girer ve her bir loopta okuma yapar.
        
        
        if(millis()- baslangiczamani < mysistem.myAds.adc_readtime[itr]){//readtime kadar okur
          adc_toplayici += mysistem.myAds.ads.readADC_SingleEnded(itr);//adc okuma kısmı
          adc_read_counter++;
          Serial.print("cp3 ");
        }else {
          dongu_read_finish[itr]=true;
          mysistem.myAds.adc[itr]=adc_toplayici/adc_read_counter;
          gonderilecek_json = makeJsonPayload(mysistem.myAds.adc);
          sendJsonPayload(gonderilecek_json);
          Serial.print(gonderilecek_json);
          Serial.print(itr);
        }
      }
      else if(!dongu_delay[itr]){//dongu delay süreci
        if(millis()-prev > mysistem.myAds.adc_delay[itr]&& !dongu_delay[itr]){
          //delay süresi bittiyse dongu_delay[itr] true yap. ve okuma işlemi başlayabilir.
          dongu_delay[itr]=true;
          baslangiczamani = millis();Serial.print("cp5 ");
        }
      }
      if(dongu_led[itr] && dongu_delay[itr] && dongu_read_finish[itr]){
        dongu_led[itr]=false;
        dongu_delay[itr]=false;
        dongu_read_finish[itr]=false;
        starter_controller=false;
        if(itr == 3)itr=0;
        else itr++;
        Serial.print("cp4");//debug
      }
    }
    

    
    
  }else{
    //ble bağlantısı yoksa
    if(mysistem.myAds.is_adc_started){
      //bluetooth bağlantısı gerçekleşmemiştir bunun anlaşılabilir olması için ve adc ile kart arassında I2C haberleşmesinin başladığını kabul ederek
      //adc led debugging metodu ile karışmaması için LED sürekli yanar eğer bluetooth bağlantısı yoksa.
      digitalWrite(LED_BUILTIN,HIGH);
    }
    
  }


}

String makeJsonPayload(int values[4]) { // Fixed parameter types
  String s = "[";
  for (int i = 0; i < 4; ++i) {
    s += "[";                   // başlangıç indeks
    s += "\"";
    s += "mV";                  // birim string
    s += "\"";
    s += ",";
    s += String(millis()); // timestamp
    s += ",";
    s += String(((float)values[i] / 32768.0) * 2.048 * 1000.0);       // mV
    s += ",";
    s += String(values[i]);    // değer
    s += "]";
    if (i < 3) s += ",";       // indeks ayırıcı
  }
  s += "]";
  s += "\n"; // newline ile bitiriyoruz, Python tarafı buna göre parse edecek
  return s;
}


void sendJsonPayload(const String &payload) { // Fixed parameter type
  // bleuart.write bekler; String.c_str() ile char* veriyoruz
  bleuart.write((uint8_t*)payload.c_str(), payload.length());
  // debug
  //Serial.print("Sent JSON: ");
  //Serial.print(payload);
}

// callback invoked when central connects
void connect_callback(uint16_t conn_handle)
{
  BLEConnection* connection = Bluefruit.Connection(conn_handle);
  char central_name[32] = { 0 };
  connection->getPeerName(central_name, sizeof(central_name));
  Serial.print("Connected to ");
  Serial.println(central_name);
  myleds.toggleFlag[4]=true;//mikroişlemci bluetooth bağlantısını gerçekleştirdiyse led kapalı kalır.
}

void disconnect_callback(uint16_t conn_handle, uint8_t reason)
{
  (void) conn_handle;
  (void) reason;
  Serial.println();
  Serial.print("Disconnected, reason = 0x"); Serial.println(reason, HEX);
}

void startAdv(void)
{
  Bluefruit.Advertising.addFlags(BLE_GAP_ADV_FLAGS_LE_ONLY_GENERAL_DISC_MODE);
  Bluefruit.Advertising.addTxPower();
  Bluefruit.Advertising.addService(bleuart);
  Bluefruit.ScanResponse.addName();
  Bluefruit.Advertising.restartOnDisconnect(true);
  Bluefruit.Advertising.setInterval(32, 244);
  Bluefruit.Advertising.setFastTimeout(30);
  Bluefruit.Advertising.start(0);
}

void uart_rx_callback(uint16_t conn_handle) {
    (void) conn_handle;
    static bool json_started = false;
    static int brace_count = 0;

    while (bleuart.available()) {
        char c = (char)bleuart.read();
        // Détecter le début d'un JSON
        if (c == '{' && !json_started) {
            json_started = true;
            brace_count = 0;
            rxBuffer = ""; // Vider le buffer pour un nouveau message
        }
        
        if (json_started) {
            rxBuffer += c;
            
            if (c == '{') brace_count++;
            if (c == '}') brace_count--;
            
            // JSON complet détecté
            if (brace_count == 0 && rxBuffer.length() > 2) {
                callback = true;
                json_started = false;
                break;
            }
        }
    }
}

void parseJsonBuffer(const String &buffer) {
  if (buffer.length() == 0) return; // boş string geldiyse çık

  // CORRECTION : Nettoyer le buffer des caractères de contrôle et newlines
  String cleanBuffer = buffer;
  cleanBuffer.trim(); // Enlever les espaces et newlines au début/fin
  
  // Optionnel : debug pour voir ce qu'on reçoit
  Serial.print("Received buffer length: ");
  Serial.println(cleanBuffer.length());
  Serial.print("Buffer content: ");
  Serial.println(cleanBuffer);

  StaticJsonDocument<2048> doc;
  DeserializationError err = deserializeJson(doc, cleanBuffer); // Utiliser cleanBuffer

  if (err) {
    Serial.print("JSON parse error: ");
    Serial.println(err.f_str());
    return;
  }

  // sequences kısmını al
  if (doc.containsKey("sequences") && doc["sequences"].is<JsonArray>()) {
    JsonArray arr = doc["sequences"].as<JsonArray>();

    for (JsonObject obj : arr) {
      int pin      = obj["led_pin"]      | -1;
      int openMs   = obj["time_open_ms"] | 0;
      int delayMs  = obj["time_delay_ms"]| 0;
      int readMs   = obj["time_read_ms"] | 0;
      bool enabled = obj["enabled"]      | false;

      Serial.print("Pin: "); Serial.print(pin);
      Serial.print(" open: "); Serial.print(openMs);
      Serial.print(" delay: "); Serial.print(delayMs);
      Serial.print(" read: "); Serial.print(readMs);
      Serial.print(" enabled: "); Serial.println(enabled);

      pin--;
      if (enabled && pin >= 0 && pin < 4) {
        mysistem.state = true;
        myleds.leds[pin].kalansure       = openMs;
        mysistem.myAds.adc_delay[pin]    = delayMs;
        mysistem.myAds.adc_readtime[pin] = readMs;
        mysistem.myAds.read_state[pin]   = true;

        handle(pin, openMs * 1000);
        digitalWrite(myleds.pinNos[pin], HIGH);
      } 
      else if (pin >= 0 && pin < 4) {
        mysistem.myAds.read_state[pin] = false;
        timer_interrupt_clr(pin);
      }
    }
  } else {
    Serial.println("JSON içinde sequences bulunamadı!");
  }
}


void timer_start_handle(){
  NRF_TIMER1->TASKS_START = 1;
}

void timer_stop_handle(){
  NRF_TIMER1->TASKS_STOP = 1;
}

void timer_interrupt_set(int id){
  
  NRF_TIMER1->TASKS_CLEAR=1;
  switch(id){
    case 0:
    NRF_TIMER1->INTENSET = TIMER_INTENSET_COMPARE0_Msk;
    break;
    case 1:
    NRF_TIMER1->INTENSET = TIMER_INTENSET_COMPARE1_Msk;
    break;
    case 2:
    NRF_TIMER1->INTENSET = TIMER_INTENSET_COMPARE2_Msk;
    break;
    case 3:
    NRF_TIMER1->INTENSET = TIMER_INTENSET_COMPARE3_Msk;
    break;
    default:
    break;
  }
}



void timer_interrupt_clr(int id){
  switch(id){
    case 0:
    NRF_TIMER1->INTENCLR = TIMER_INTENSET_COMPARE0_Msk;
    break;
    case 1:
    NRF_TIMER1->INTENCLR = TIMER_INTENSET_COMPARE1_Msk;
    break;
    case 2:
    NRF_TIMER1->INTENCLR = TIMER_INTENSET_COMPARE2_Msk;
    break;
    case 3:
    NRF_TIMER1->INTENCLR = TIMER_INTENSET_COMPARE3_Msk;
    break;
    default:
    break;
  }
}


void handle(int8_t led_id,uint32_t time_open_v){
  NRF_TIMER1->TASKS_STOP = 1;
  NRF_TIMER1->TASKS_CLEAR = 1;
  NRF_TIMER1->CC[led_id]=time_open_v;
  NRF_TIMER1->TASKS_START = 1;
}


void printResetReason() {
  uint32_t reas = NRF_POWER->RESETREAS;
  Serial.print("RESETREAS = 0x"); Serial.println(reas, HEX);
  // clear
  NRF_POWER->RESETREAS = reas;
}



/*
gelen veri yapısı
state
timeopen led'in yanma süresi ms
timedelay led yanmaya başladıktan timedelay ms sonra adc verisini okumaya başla
time read   
pin (0,1,2,3)
*/
